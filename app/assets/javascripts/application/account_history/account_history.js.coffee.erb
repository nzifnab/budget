class AccountHistory
  @className: "AccountHistory"

  @renderSidebar: (html, sidebarLabel, tabTooltip) ->
    $(".sidebar").removeClass("hide")
    $(".sidebar-content").html(html)
    $(".sidebar-tab").removeClass("mobile-hide")
    $(".sidebar-tab a").text(sidebarLabel) if sidebarLabel?
    $(".sidebar-tab a").attr("title", tabTooltip) if tabTooltip?

  @fetchHistoryForAccount: (accountId) ->
    $.get(
      "<%= Rails.application.routes.url_helpers.account_histories_path(format: 'json') %>",
      {account_id: accountId},
      (data, textStatus, xhr) =>
        @renderSidebar(data.html, data.accountNameShort, data.accountName)
    )

  @events: =>
    $(".js-account-accordion").on "accordionbeforeactivate", (e, ui) =>
      #ui.newHeader, ui.oldHeader, ui.newPanel, ui.oldPanel
      if ui.newHeader.length > 0 && ui.newPanel.length > 0
        @fetchHistoryForAccount(ui.newHeader.data("account-id"))

budget.register AccountHistory
